apply plugin: 'groovy'
apply plugin: 'nexus'

group = 'com.github.ben-manes'
version = '0.1'

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'com.googlecode.jsonschema2pojo:jsonschema2pojo-cli:0.3.6'
}

jar {
  manifest {
      attributes 'Implementation-Title': 'Gradle Json Schema to Pojo plugin',
                 'Implementation-Version': version,
                 'Built-By': System.getProperty('user.name'),
                 'Built-Date': new Date(),
                 'Built-JDK': System.getProperty('java.version'),
                 'Built-Gradle': gradle.gradleVersion
  }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc.destinationDir
}

artifacts {
  archives groovydocJar
}

modifyPom {
  project {
    name 'Gradle Json Schema to Pojo plugin'
    description 'JsonSchema codegen for Gradle.'
    url 'https://github.com/ben-manes/gradle-jsonschema2pojo-plugin'
    inceptionYear '2013'
  
    scm {
      url 'https://github.com/ben-manes/gradle-jsonschema2pojo-plugin'
      connection 'scm:https://ben-manes@github.com/ben-manes/gradle-jsonschema2pojo-plugin.git'
      developerConnection 'scm:git://github.com/ben-manes/gradle-jsonschema2pojo-plugin.git'
    }
  
    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
  
    developers {
      developer {
        id 'ben-manes'
        name 'Ben Manes'
        email 'ben.manes@gmail.com'
      }
    }
  }
}

task wrapper(type: Wrapper, description: 'Generates the gradle wrapper.') {
  gradleVersion = '1.6-rc-1'
}
